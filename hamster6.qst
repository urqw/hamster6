:10
instr gametitle=Похождения отважного хомяка Семена-6: В Роще
pln #%gametitle$
btn introduction, Начать игру
btn autors, Автор(ы)
btn instructions, Инструкции
end

:autors
pln Идея____________Triangle
pln Программист_____Apromix, Simple
pln Сценарий________Apromix, Simple
pln Наборщик________Apromix, Simple
pln В главной роли__Ныне покойный хомяк Семен
proc contacts
btn 10, Назад
end

:contacts
pln
pln Пишите человеку который придумал Семена на triangle@zmail.ru 
pln Пишите Simple на simpleurq@mail.ru
pln Пишите Apromix'у на apromix@mail.ru или bees@meta.ua
end

:instructions
pln Тут инструкции не нужны! И так все понятно!
btn 10, Назад
end

:introduction
pln Вы - хомяк по кличке Семен. Если Вы прошли предыдущие игры из серии, то должны помнить, что Вас коварно заточили в клетку, но Вам удалось вырваться на свободу и покинуть дом и двор, перемахнув через забор. Но и здесь злопамятный кот не оставил Вас в покое. В конце-концов Вам удалось обмануть кота и убежать от него в рощу... Пока все.
btn game, Играть!
perkill
invkill
kot = 1
end

:game
pln Возле дома всегда была роща и дорога, но Вы этого не могли знать, так как всю свою жизнь провели в тесной клетке. Она казалась большим уютным миром, но после побега мир вырос во много раз и ограничивался только стенами дома. Казалось, мир на этом перестанет расширяться, но за домом открылось невообразимо огромное пространство, большую часть которого занимали дорога и роща...
:game2
pln Внезапно послышался подозрительный звук. Вы замерли, а затем снова прислушались, но уловили только шум ручья.
if tt = 0 then btn look, Осмотреться
btn road, Дальше бежать по дороге
btn tree, Спрятаться на дереве
btn grove, Бежать на шум воды
btn grove2, Прокрадываться в сторону ручья
end

:grove
p Теперь Вы бежите среди огромных деревьев мимо ручья и с нескрываемым удовольствием вдыхаете запах свободы...
if nn = 0 then proc death&end
pln&pln Неожиданно среди листвы Вы замечаете кота, готового наброситься на Вас. Старый враг ни на минуту не покидал Вас и только искал момент, чтобы покончить с Вами раз и навсегда.
pln Внимательность помогает Вам избежать встречи с еще одним опасным врагом - совой! Она сидит на вершине осины и пристально наблюдает за каждым Вашим движением.
btn look2, Осмотреться
btn road, Вернуться на дорогу
btn brook, Прыгнуть в ручей
btn tree, Взобраться на дерево
end
:grove2
nn = 1
goto grove
end

:look
p В траве Вы замечаете БОльшую жевачку.
if Жевачка then pln #$У Вас уже есть один кусочек. 
if not Жевачка then pln #$Вы отгрызли себе кусочек на память.&inv+ Жевачка
tt = 1
goto game2
:look2
pln Благодаря своей внимательности Вы не угодили в ловушки, так умело расставленные врагами. Но что делать теперь?
pln Вы находитесь на берегу ручейка, где только камни, мох и низкая трава. Запах мышей отчетливо улавливается во влажном воздухе рощи, но нигде не видно ни одной мышачьей норки, в которой можно было бы спрятаться. Только высокие деревья и старое гнилое бревно у ручья. 
btn road, Выбежать на дорогу
btn log, Спрятаться под бревном
btn brook, Переплыть ручей
btn tree, Взобраться на дерево
end

:log
pln Вы прячетесь под бревном. Коту не удасться залезть под бревно. Осознав этот факт, Вы мирно засыпаете. Во сне у Вас зреет план побега из рощи на плоту...
btn log2, Проснуться
end

:log2
day = 1
loc="log2"
if rr = 0 then p Вы просыпаетесь глубокой ночью.&rr = 1 else p Ночь.
p #$В траве стрекочут сверчки. Взошла луна.
btn log3, Снова лечь спать
btn ulog, Осмотреться
btn road, Бежать на дорогу
btn brook, Перебраться через ручей
btn tree, Взобраться на дерево
end

:log3
kot = 0
day = 0
loc="log3"
if rr = 1 then p Вы просыпаетесь поздним утром.& rr = 0 else p Позднее утро.
p #$Солнце уже давно взошло. Бабочки и стрекозы летают над ручейком.
if not Щепка then p #$Под ногами у Вас лежит щепка.&btn sliver, Взять щепку
btn log2, Снова лечь спать
btn ulog, Осмотреться
btn road, Бежать на дорогу
if Щепка and Лист and Жевачка then btn brook, Спуститься на берег else btn brook, Прыгнуть в ручей и переплыть на другую сторону
btn tree, Взобраться на дерево
end

:sliver
if not Бумажка then pln Вы запихиваете острую щепку за щеку, и она ее с легкостью протыкает. Вы быстро истекаете кровью...&btn introduction, Сначала&end
if Бумажка then pln Вы заворачиваете щепку в бумажку и только после этого запихиваете щепку за щеку.
inv- Бумажка
inv+ Щепка
btn log3, Назад
end

:ulog
pln У бревна два конца. С какого конца выглянуть и осмотреться?
btn nlog, Осмотреться с северной стороны
btn slog, Осмотреться с южной стороны
btn #%loc$, Больше не осматриваться
end

:nlog
p Вы осторожно выглядываете из-под бревна.
if day = 0 then p #$Опасность миновала. Коту надоело Вас сторожить у ручья и он, кажется, ушел домой.&btn ulog, Спрятаться под бревном else proc death&end
if not Лист then p #$На соседнем дереве Ваше внимание привлекают нужной формы листья.&btn tree, Лезть на дерево
end

:slog
p Вы осматриваете рощу с южной стороны
if day = 1 then p .&proc death&end else p #$и ничего не замечаете. Роща свободна от врагов! Но так ли это?&btn ulog, Спрятаться под бревном
if not Бумажка then p #$Рядом валяется скомканый бумажный лист.&btn bum, Взять бумажку
end

:bum
pln Вы отгрызаете кусочек бумажки и запихиваете его за щеку.
inv+ Бумажка
btn slog, Осмотреть рощу
end

:brook
if Щепка and Лист and Жевачка then inv- Щепка&inv- Лист&inv- Жевачка&p Вы выкладываете на берегу щепку, лист и жевачку. У Вас есть все необходимое, чтобы сделать плот.&btn plot, Сделать плот&end
p Вы бросаетесь в холодную воду и быстро работая лапками, плывете на другую сторону.
if day = 0 and kot = 1 then proc death
if day = 0 and kot = 0 then p #$Внезапно большая волна накрывает Вас с головой. Вы быстро идете ко дну, пуская пузыри...&btn introduction, Сначала
if day = 1 then proc death
end

:plot
if day = 0 and kot = 0 then pln Сложив щепку и лист вместе, Вы склеиваете их жевачкой. Плот готов.&btn water, Спустить плот на воду&end
p Вы беспечно копошитесь возле ручья, потеряв бдительность.&proc death
end

:tree
p Вы взбираетесь на ближайшее дерево.
if day = 1 or kot = 1 then proc death&end
if not Лист then p #$Вот этот лист подойдет лучше всех!&btn lv, Достать лист else p #$Но зачем?&btn #%loc$, Слезть с дерева и спрятаться
end

:lv
pln Вы достаете лист, свертываете в трубочку и запихиваете за щеку.
inv+ Лист
btn #%loc$, Слезть с дерева и спрятаться
end

:road
p Вы бежите по дороге прочь от дома и рощи.&proc death
end

:death
p #$Но внезапно краем глаза замечаете тень...
p #$К сожалению Вы так и не поняли, что стали добычей
if day = 0 then p #$кота...
if day = 1 then p #$совы...
btn introduction, Сначала
end

:water
pln Вы спускаете эту шаткую конструкцию на воду и запрыгиваете на импровизированный плот. Быстрое течение уносит Вас все дальше и дальше...
btn final, Конец
end

:final
pln Поздравляю! Вы благополучно миновали все опасности и прошли шестую серию приключений хомяка Семена! Благодаря Вам хомяк Семен остался жив! Ждите следущие серии!
proc contacts
end